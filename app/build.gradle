import com.android.ide.common.signing.KeystoreHelper
import org.apache.commons.codec.digest.DigestUtils

plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

Properties properties = new Properties()
File localPropFile = project.rootProject.file("local.properties")
if (localPropFile.exists()) {
    properties.load(localPropFile.newDataInputStream())
}

android {
    compileSdk 33
    buildToolsVersion '33.0.0'
    namespace "rikka.safetynetchecker"
    signingConfigs {
        sign {
            storeFile properties.getProperty("storeFile") != null ? file(properties.getProperty("storeFile")) : android.signingConfigs.debug.storeFile
            storePassword properties.getProperty("storePassword", android.signingConfigs.debug.storePassword)
            keyAlias properties.getProperty("keyAlias", android.signingConfigs.debug.keyAlias)
            keyPassword properties.getProperty("keyPassword", android.signingConfigs.debug.keyPassword)
        }
    }
    defaultConfig {
        minSdk 21
        targetSdk 33
        versionCode version_code
        versionName version_name
        buildConfigField "String[]", "API_KEY", "{\"${properties.getProperty("apiKey", "")}\"}"
        buildConfigField "String", "SAMPLE", "\"${property("sampleResult")}\""
        def info = KeystoreHelper.getCertificateInfo(
                signingConfigs.sign.storeType,
                signingConfigs.sign.storeFile,
                signingConfigs.sign.storePassword,
                signingConfigs.sign.keyPassword,
                signingConfigs.sign.keyAlias
        )
        def digest = Base64.encoder.encodeToString(DigestUtils.sha256(info.certificate.encoded))
        buildConfigField "String", "certificateDigest", "\"$digest\""
    }
    buildTypes {
        debug {
            signingConfig signingConfigs.sign
        }
        release {
            signingConfig signingConfigs.sign
            minifyEnabled true
            shrinkResources true
            proguardFiles 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
        coreLibraryDesugaringEnabled true
    }
    kotlinOptions {
        jvmTarget = '11'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }
    packagingOptions {
        resources {
            excludes += '**'
        }
    }
    dependenciesInfo {
        includeInApk false
    }
    lint {
        checkReleaseBuilds false
    }
}

android.applicationVariants.all { variant ->
    variant.outputs.all {
        outputFileName = "yasnac-${variant.versionName}-${variant.name}.apk"
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.8.0'
    implementation 'androidx.activity:activity-compose:1.4.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.4.1'

    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    implementation "androidx.compose.runtime:runtime-livedata:$compose_version"

    implementation "com.google.accompanist:accompanist-insets-ui:$accompanist_version"
    implementation "com.google.accompanist:accompanist-systemuicontroller:$accompanist_version"

    implementation 'com.google.android.gms:play-services-safetynet:18.0.1'
    implementation('com.google.http-client:google-http-client-android:1.42.0') {
        exclude group: 'org.apache.httpcomponents'
    }

    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"
    debugImplementation "androidx.compose.material:material-icons-extended:$compose_version"

    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'
}

configurations.all {
    exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib-jdk7'
    exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib-jdk8'
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions {
        freeCompilerArgs += "-opt-in=kotlin.RequiresOptIn"
    }
}
